allprojects {
	apply plugin:'java-library'
	apply plugin:'eclipse'
	
	repositories {
		mavenCentral()
	}
	
	project.group = 'org.zkovari.changelog'
	
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
}

subprojects {
	project.version = project.rootProject.version
	
	apply plugin:'jacoco'
	
	jacoco {
		toolVersion = "0.8.3"
	}
	
	jacocoTestReport {
		reports {
			xml.enabled true
			csv.enabled true
			html.enabled true
		}
	}
	
	sourceSets {
    	integTest {
	        java.srcDir file('src/integTest/java')
        	resources.srcDir file('src/integTest/resources')
        	
        	compileClasspath += sourceSets.main.output + sourceSets.test.output + configurations.testRuntimeClasspath
            runtimeClasspath += output + compileClasspath
    	}
		
		functTest {
			java.srcDir file('src/functTest/java')
			resources.srcDir file('src/functTest/resources')
			
			compileClasspath += sourceSets.main.output + sourceSets.test.output + configurations.testRuntimeClasspath
			runtimeClasspath += output + compileClasspath
		}
	}

	task integTest(type: Test) {
		group = 'verification'
		description = 'Runs the integration tests.'
    	
	    testClassesDirs = sourceSets.integTest.output.classesDirs
	    classpath = sourceSets.integTest.runtimeClasspath
	    mustRunAfter tasks.test
		
		jacoco {
			enabled=true
			destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
			classDumpDir = file("$buildDir/jacoco/classpathdumps")
		}
	}
	
	task functTest(type: Test) {
		group = 'Verification'
		description = 'Runs the functional tests.'
		
		testClassesDirs = sourceSets.functTest.output.classesDirs
		classpath = sourceSets.functTest.runtimeClasspath
		mustRunAfter tasks.test
		
		jacoco {
			enabled=true
			destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
			classDumpDir = file("$buildDir/jacoco/classpathdumps")
		}
	}
	
	check.dependsOn integTest
	check.dependsOn functTest
	
	eclipse {
        classpath {
            plusConfigurations += [
                configurations.integTestCompile,
				configurations.functTestCompile,
            ]

        }
    }
	
	test {
		testLogging {
			events "failed"
			exceptionFormat "full"
		}
		
		jacoco {
			destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
			classDumpDir = file("$buildDir/jacoco/classpathdumps")
		}
		
	}

}